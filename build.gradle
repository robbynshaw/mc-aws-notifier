buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle' version '2.4.3'
    id 'io.freefair.lombok' version '6.6.3'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 17
targetCompatibility = 17

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    // depends = ['']
    // softDepends = ['Vault']
    excludeLibraries = ['*']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    // options.forkOptions.executable = '/usr/lib/jvm/msopenjdk-current/bin/javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    spigot()
    bungeecord()
    paper()
    essentialsX()
    bStats()
    enginehub()
    protocolLib()
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    jitpack()
}

dependencies {
    // using using paper-api
    implementation paper(mcVersion)

    implementation "com.google.code.gson:gson:2.11.0"

    implementation platform("software.amazon.awssdk:bom:2.28.11")
    implementation 'software.amazon.awssdk:ecs'
    implementation 'software.amazon.awssdk:ec2'
    implementation 'software.amazon.awssdk:route53'

    // Annotation Command Framework: https://github.com/aikar/commands
    // Use shadow to shade the library into your plugin
    shadow "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.144.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

shadowJar {
    // classifier = ''
    configurations = [project.configurations.runtimeClasspath, project.configurations.shadow]
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
